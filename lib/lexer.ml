(* ░▓▓██████████████████████▓▓▓▓▓▓███████████████
                ░▓███████▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓███████████████
                ░▓█▓█▓▓▓▓▓▓▓▓▓▓▓▓▓▓▒▒▒▒▒▓▓▓▓▓▓▓▓▓▓▓███████████
                ░▓█▓▓▓▓▓▓▓▓▓▓▓▓▒▓▓▓▒▒▒▒▒▒▒▒▓▓▓▓▓▓▓▓▓██████████
                ░▓█▓▓▓▓▓▓▓▓▓▓▒▓▓▓▓▓▓▓▓▓▓▓▒▓▒▒▒▓▓▓▓▓▓██████████
                ░▒▓▓▓▒▓▓▓▓▓▓▓▓▓▒▓▓▓▓▓▓▓▓▓▓▓▓▓▒▒▓▓▓▓▓▓█████████
                ▒▓▓▓▓▓▓▓▓▓▓▒▒▓▒▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▒▒▓▓▓▓▓█████████
               ▒▓▓██▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▒▓▓▓▓▒▒▒▒▒▓▓▓▓▓▓██████████
              ▒▓▓▓██▓▓▓▓▓▓▓▓▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▓▓▓▓██████████
            ░▓▓▓▓▓▓▓▓▓▓▓▓▓▒▒▒▒▒░░░░░░░░░░░░░░░░░▒▒▒▓▓█████████
            ▒▓▒▒▓▓▓▓▒▒▒▒▒▒▒░░░░░░              ░░░░▓▓▓████████
           ░▓▒▒▒▒▒▒▒▒░░░░░░░░                      ░▒▓▓█████▓█
           ▒▒▒▒▒▒▒▒▒░░░░░░░                         ░▓▓▓▓█▓▓▓▓
           ▒▒▒▒▒▒▒▒▒░░░░░░                        ░░░░░▒▓▓█▓▓▓
          ░▒▓▒▒▒▒▒▒▒▒░░░░░                        ░░░░░░▓█▓▓▓▓
          ░▓▓▒▓▒▒▒▒▒▒▒░░░                   ░░░░░░░░░░▒░▒▓▓▓▓▓
          ▒▓▓▓▓▒▒▒▒▒▒░░░ ░░░              ░░░ ░░░░░░░░▒▒▒▓▓▓▓▓
          ▒▓▓▓▓▓▒▒▒▒▒░░░    ░░░░       ░░░░░░░░  ░░░░░▒▓▓▓████
          ░▓▓▓▓▒▒▒▒▒▒▒░░░░▒▒░░░░░░   ░░▒▒▒▓▒▒▒▒░░░░░░▒▒▓▓█████
           ▒▓▓▓▒▒▒▒▒▒▒▒▒▒▒▒▓▓▓▓▒░░   ░░▒▒▒▒▒░░░░░░ ░░▒▒▒▓█████
           ░▓▓▓▒▒▒▒▒▒▒▒▒▒░░░░░▒▒░      ░░░░░░░      ░▒░░▓█████
           ░▓▓▓▓▒▒▒░▒░░░     ░░░                   ░░▒  ▓█████
           ░▒▓▓▓▒▒▒▒░░░░     ░░░░                  ░░░ ░██████
            ▒▓▓▓▓▒▒▒▒▒░░░    ░▒░                   ░░  ▒██████
            ░▒▓▓▓▓▒▒▒▒▒░░   ░░▒░░                 ░░░  ▒██████
             ▒▓▓▓▓▓▒▒▒▒▒░░░ ░▒▓▓▒▒░░▒▒▒          ░░▒░ ░███████
              ░▒▓▓▓▓▒▒▒▒░░░ ░▒▒▓▓▓▒▒▒░         ░░░░▒▒▒▓███████
               ▒▓▓▓▓▓▒▒▒░░░░░▒▒▒░▒▒▒▒░░        ░░░▒▓▓█████████
               ▒▒▓▓▓▓▓▓▒▒░░░▒▒▒▒░░▒▒▒░░░░    ░░░░▒▓▓██████████
               ▒▓▓▓▓▓▓▓▓▒▒▒▒▓▓▓▓▓▓▓▒▒░░░░░░░░░░▒▒▒▓▓██████████
              ░▒▓▓▓▓▓▓▓▓▓▓▓▓▒▒▒▒███▓▒▒▒▒▒░░░░▒▒▒▒▓▓▓██████████
           ▒▒▒▒▓▓▓▓▓▓▓▓▓▓▓▒▒▒▒░░▒▒▒▒░░░▒▒▒░░▒▒▓▓▓▓░░░▓████████
        ░░▒▒▒▒▓▓▓▓▓███▓▓▓▒▒▒▒▓▒░░░░░░░░░▒░░▒▒▓▓▓▓▓▒░   ░▒▒▓███
   ░░░░▒▒▒▓▓▓▓▒▒▒▓▓██▓▓▓▓▓▓▒▒▒▓▓▒▒▒▒▒░░░▒▒▒▒▓▓▓▓▓▓▒          ▒
   ░░░░░▒▒▓▓▓▓▓▒▒█▓███▓▓▓▓▓▒▒▒▒▒▒▒▒░░░░▒▒▒▒▓▓▓▓▓▓▒      ░░
      ░░░▒▒▓▓▓▒▒▒▓▓▓▓▓▓█▓▓▓▓▒▒▒▒▒▒░░░░░▒▒▒▓▓▓▓▓▓▒░
      ░░░░▒▒▓▓▒▒▓▓▓▓▓▓▓██▓▓▓▓▒▒▒▒▒░░░░▒▒▓▓▓▓▓▓▓▒     ░░░░ ░░
     ░░░▒▒▒▒▓▓▒▒▓▓▓▓▓▓▓███▓▓▓▓▒▒▒▒▒▒▒▒▓▓▓▓▓▓▓▓░        ░░░ ░░░
        ░░░░▒▓▒▒▒▓▓▓▓▓▓█████▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▒░░            ░░░
           ░░▒▒▒▒▓▓▓▓▓▓▓▓███████████████▓▓▒▒░░               ░
           ░░▒▒▒▒▓▓▓▓▓▓▓▓▓▓▓▓████████▓▓▓▓▒░░░░             ░░
         ░░▒▒░▒▒▒▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▒▒▒▒░░░░░░         ░░░░▒░
     ░░░░░░░░▒░▒▒▒▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▒▒▒▒▒░░░░░░░░    ░░░░░    ░ *)

open Prelude
open Span

type token =
  | Ident of string
  | String of { raw : string; value : string }
  | Punctuation of string
  | Number of string

let raw = function
  | Ident raw | Punctuation raw | Number raw | String { raw; _ } -> raw

type position_with_index = { pos : pos; index : int }

let show : token -> string = raw

let show_spanned (token : token spanned) : string =
  let span = token.span in
  "at " ^ Span.show span ^ " (" ^ show token.value ^ ")"

let parse : string -> filename -> token spanned Seq.t =
 fun contents file ->
  let content_len = String.length contents in
  let index = ref 0 in
  let position = ref start_pos in
  let peek : unit -> char option =
   fun () ->
    if !index < content_len then Some (String.get contents !index) else None
  in
  let next : unit -> char option =
   fun () ->
    let c = peek () in
    (match c with Some _ -> index := !index + 1 | _ -> ());
    position := { !position with column = !position.column + 1 };
    (match c with
    | Some '\n' -> position := { line = !position.line + 1; column = 0 }
    | _ -> ());
    c
  in
  let consume () =
    let _ = next () in
    ()
  in
  let read_while (f : char -> bool) : string =
    let rec impl () =
      match peek () with
      | Some c when f c ->
          consume ();
          c :: impl ()
      | _ -> []
    in
    String.of_seq (List.to_seq (impl ()))
  in
  let rec skip_whitespace () =
    let _ = read_while is_space in
    if peek () = Some '#' then (
      while match peek () with Some '\n' | None -> false | _ -> true do
        consume ()
      done;
      skip_whitespace ());
    ()
  in
  let read_string () =
    let start = !index in
    if next () <> Some '"' then failwith "expected \"";
    let value = read_while (fun c -> c <> '"') in
    if next () <> Some '"' then failwith "expected \"";
    String { value; raw = String.sub contents start (!index - start) }
  in
  let read_number () =
    Number (read_while (fun c -> is_digit c || c = '.' || c = '_'))
  in
  let is_ident_start c = is_alpha c || c = '_' in
  let read_ident () =
    Ident (read_while (fun c -> is_ident_start c || is_digit c))
  in
  let is_punctuation c =
    (not (is_alphanumeric c)) && (not (is_space c)) && c <> '\'' && c <> '"'
  in
  let is_single_punctuation c = Option.is_some (String.index_opt "()[]{}" c) in
  let read_punctuation () =
    let c = Option.get (peek ()) in
    if is_single_punctuation c then (
      consume ();
      Punctuation (String.make 1 c))
    else
      Punctuation
        (read_while (fun c -> is_punctuation c && not (is_single_punctuation c)))
  in
  let next_token () : token spanned option =
    skip_whitespace ();
    match peek () with
    | None -> None
    | Some c ->
        Some
          (let start = !position in
           let token =
             if c = '"' then read_string ()
             else if c = '@' then (
               consume ();
               match read_string () with
               | String { value; _ } -> Ident value
               | _ -> failwith "read not a string?")
             else if is_digit c then read_number ()
             else if is_ident_start c then read_ident ()
             else read_punctuation ()
           in
           { value = token; span = { start; finish = !position; file } })
  in
  Seq.of_dispenser next_token
